
load sys;
load graphics;
load test_tiger_paths;

routine LoadTiger( scene : GraphicsScene )
{
	for( i = 0 : pathCount-1 ){
		commands = commandArrays[i]
		points = dataArrays[i]
		style = styleArrays[i];

		scene.PushState()
		scene.SetStrokeColor( (style[0], style[1], style[2]), style[3] )
		scene.SetFillColor( (style[4], style[5], style[6]), style[7] )

		# vgDrawPath(path,paintModes), style[9] is the paintModes:
		strokeWidth = (((int)style[9]) & 1) ? style[8] : 0;
		scene.SetStrokeWidth( strokeWidth )
		
		path = scene.AddPath();
		k = 0;
		for( j = 0 : commandCounts[i]-1 ){
			switch( commands[j] ){
			case VG_MOVE_TO_ABS  :
				path.MoveTo( points[k], points[k+1] );
				k += 2;
			case VG_LINE_TO_ABS  :
				path.LineAbsTo( points[k], points[k+1] );
				k += 2;
			case VG_CUBIC_TO_ABS :
				path.CubicAbsTo( points[k], points[k+1], points[k+2], points[k+3], points[k+4], points[k+5] );
				k += 6;
			case VG_CLOSE_PATH   :
				path.Close();
			}
		}
		scene.PopState()
	}
}

routine main( fps_limit = 30, compute_fps = 0 )
{
	scene = GraphicsScene();
	scene.SetBackground( (1.0,1.0,1.0), 0.0 );
	scene.SetTransform( [-1.0, 0, 0, -1.0, 100, 100] );
	LoadTiger( scene );
	glutInit( 1000, 800, 'Dao Logo', fps_limit, compute_fps )
	glutShow( scene )
}
